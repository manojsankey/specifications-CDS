= Overview

== Background

Expressing and sharing computerized clinical decision support (CDS) content across languages and technical platforms has been an evasive goal for a long time. Lack of commonly shared clinical information models and flexible support for various terminology resources have been identified as two main challenges for sharing decision logic across sites.

== Scope

The scope of the GDL is to express clinical logic as production rules. Discrete GDL rules, each containing if-then statements, can be combined together as building blocks to support single decision making and more complex decision making processes. The GDL rules can be used to drive at-point-of-care decision support applications as well as retrospective populational analytics.

== An Example

The following is a simple GDL example that allows us to calculate CHA2DS2-VASc Score, a clinical tool for stroke risk stratification in atrial fibrillation.

GDL Guidelines are expressed in the {openehr_odin}[openEHR ODIN^] format (similar to JSON with typing), such that a GDL source is a serialised instance of the, GDL2 Object Model, described below in <<_gdl2_object_model>>.

The meta-data in the GDL header is based on the {openehr_resource}[openEHR Resource model^]. The following GDL source illustrates the current version of the guideline, the supported natural languages, to which it has been translated, authorship information, lifecycle state, keywords and the purpose, use and misuse of the guideline.

NOTE: The term 'guideline' is used as a short name for a GDL guideline document in the remainder of this document.

The other top-level sections are respectively `definition` and `terminology`.

[source, odin]
----
<
    gdl_version = <"0.1">
    id = <"CHA2DS2VASc_Score_calculation.v1-Revised function">
    concept = <"gt0036">
    original_language = <[ISO_639-1::en]>
    description = <
        details = <
            ["en"] = <
                copyright = <"">
                keywords = <"Atrial Fibrillation", "Stroke", "CHA2DS2-VASc">
                misuse = <"">
                purpose = <"Calculates stroke risk for patients with atrial fibrillation, possibly better than the CHADS2 score.">
                use = <"Calculates stroke risk for patients with atrial fibrillation, possibly better than the CHADS2 score.">
            >
        >
        lifecycle_state = <"Author draft">
        original_author = <
            ["date"] = <"2012/12/03">
            ["email"] = <"rong.chen@cambio.se">
            ["name"] = <"Rong Chen">
            ["organisation"] = <"Cambio Healthcare Systems">
        >
        other_contributors = <"Carlos Valladares">
    >
    definition = <
        pre_conditions = <...>
        rules = <...>
        data_bindings = <...>
        local_variables = <...>
    >
    terminology = <
        term_definitions = <
            ["en"] = <
                ["gt123"] = <
                    text = <"some text">
                    description = "some description">
                >
            >
        >
        term_bindings = <
            ["ICD10"] = <
                ["gt123"] = <http://who.int/icd10/id/xxxx>
            >
        >
    >
>
----

The following block illustrates the `data_bindings` sub-section within the `definition` section, which binds data elements from the archetypes to variables used by GDL rules.

[source, odin]
----
definition = <
    data_bindings = <
        [1] = <
            archetype_id = <"openEHR-EHR-EVALUATION.problem-diagnosis.v1">
            domain = <"EHR">
            elements = <
                ["gt0107"] = <
                    path = <"/data[at0001]/items[at0002.1]">
                >
            >
        >
    >
>
----

Within the `definition` section, it is possible to define a set of _pre-conditions_ that have to be met before the rules inside the guideline can be executed. In the case of CHA2DS2-VASc score calculation, the guideline will not be executed unless the patient has been diagnosed with atrial fibrillation.

TODO: this is currently coded as a predicate, but should be a 

In the example below, a pre-condition checks all the diagnoses of the patient (gt0107) for the existence of atrial fibrillation. Using predicate in the `definition` section the precondition is set to check against a local code (gt0105) which represents atrial fibrillation. This code can be bound to a concept formally defined by external reference terminologies, e.g. SNOMED CT, later on in the `term_bindings` section.

TODO: At the moment is it not clear what distinguishes `pre_conditions`, `predicates` and `when` conditions. We need to improve this explanation.

[source, odin]
----
definition = <
    pre_conditions = <"$gt0107!=null">
    data_bindings = <
        [1] = <
            archetype_id = <"openEHR-EHR-EVALUATION.problem-diagnosis.v1">
            domain = <"EHR">
            elements = <
                ["gt0107"] = <
                    path = <"/data[at0001]/items[at0002.1]">
                >
            >
            predicates = <"/data[at0001]/items[at0002.1] is_a local::gt0105|Atrial fibrillation|">
            template_id = <"diagnosis_chadvas_icd10">
        >
    >
>
----

The `rules` section makes exclusive use of the locally defined variables to express the clinical logic. Each rule has a name codified by a locally defined gt-code, with which its natural language-dependent name and description are indexed in the `term_definitions` section. Also a priority can be assigned to ensure execution order of the rules. Higher priorities are executed earlier. This example illustrates rules that inspect different diagnoses relevant to CHA2DS2-VASc score and set the values of the `DV_ORDINALs` inside a CHA2DS2-VASc score archetype. The rule `gt0026` ("Calculate total score") sums the values and sets the total score in CHA2DS2-VASc score archetype.

[source, odin]
----
definition = <
    rules = <
        ["gt0018"] = <
            when = <"$gt0108!=null">
            then = <"$gt0014=1|local::at0031|Present|">
            priority = <11>
        >
        ["gt0019"] = <
            when = <"$gt0109!=null">
            then = <"$gt0010=1|local::at0034|Present|">
            priority = <9>
        >
        ["gt0026"] = <
            then = <"$gt0016.magnitude=( ( ( ( ( (gt0009.value+$gt0010.value)+$gt0011.value)+$gt0015.value)+$gt0012.value)+$gt0013.value)+$gt0014.value)">
            priority = <1>
        >
    >
>
----

Finally we have the `terminology` section of the guideline, where all the terms are bound to user interface labels and description of the terms in supported natural languages.

[source, odin]
----
terminology = <
    term_definitions = <
        ["en"] = <
            ["gt0003"] = <
                text = <"Diagnosis">
            >
            ["gt0014"] = <
                text = <"Hypertension">
            >
            ["gt0102"] = <
                text = <"Diabetes">
            >
            ["gt0105"] = <
                text = <"Atrial fibrillation">
            >
            ["gt0018"] = <
                text = <"Set hypertension">
            >
            ["gt0019"] = <
                text = <"Set diabetes">
            >
            ["gt0026"] = <
                text = <"Calculate total score">
            >
        >
    >
>
----

In addition, locally defined terms may be bound to concepts or refsets defined by external reference terminologies in `term_bindings`. In this sample, the diagnosis of atrial fibrillation is bound to a specific code in ICD10.

[source, odin]
----
terminology = <
    term_bindings = <
        ["ICD10"] = <
            ["gt0105"] = <http://who.int/icd10/id/I48>
        >
    >
>
----
